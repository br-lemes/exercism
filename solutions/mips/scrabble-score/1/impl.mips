# | Register | Usage     | Type    | Description                  |
# | -------- | --------- | ------- | ---------------------------- |
# | `$a0`    | input     | address | null-terminated input string |
# | `$v0`    | output    | integer | scrabble score               |
# | `$t0-9`  | temporary | any     | for temporary storage        |

.globl scrabble_score

.data

scores: .byte 1, 3, 3, 2, 1, 4, 2, 4, 1, 8, 5, 1, 3, 1, 1, 3, 10, 1, 1, 1, 1, 4, 4, 8, 4, 10

.text

scrabble_score:
        move    $t0, $a0
        li      $v0, 0

loop:
        lb      $t1, ($t0)
        beqz    $t1, break
        addi    $t0, $t0, 1
        ori     $t1, $t1, 32
        sub     $t1, $t1, 'a'
        lb      $t2, scores($t1)
        add     $v0, $v0, $t2
        b       loop

break:
        jr      $ra
